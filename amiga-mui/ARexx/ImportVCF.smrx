/*
This is an ARexx-Script for SimpleMail
You can only start this script from SimpleMail and not from shell.

$VER: ImportVCF.smrx 1.3 (11.12.16) $
$Author: Stefan Haubenthal
$Label: Import VCF
$Desc: Import vCard files into SimpleMail
*/

address "SIMPLEMAIL.1"

REQUESTFILE title '"Please select the visiting cards you wish to import"',
 multiselect pattern '"#?.vcf"' stem filelist.

address command "Which ConvertText >NIL:"
if rc>0 then ConvertText=";"
do i=1 to filelist.path.count
	address command ConvertText '"'value("filelist.path."i-1)'"' "T:ImportVCF SRCENC=UTF-8"
	converted=open(converttext, "T:ImportVCF")
	if open(vcf, value("filelist.path."i-1)) then
	do
		do while ~eof(vcf)
			parse value readln(vcf) with a ":" b "0d"x
			parse value a with a ";"
			if converted then
			do
				parse value readln(converttext) with . ":" b2 "0d"x
				if enc="UTF-8" then b=strip(b2)
			end
			select
				when a="BEGIN" then		addr=""
				when a="X-LOTUS-CHARSET" then	enc=b
				when a="VERSION" & b>=3.0 then	enc="UTF-8"
				when a="FN" then		addr=addr 'NAME "'||b||'"'
				when abbrev(a, "EMAIL") then	addr=addr 'EMAIL' b
				when a="NICKNAME" then		addr=addr 'ALIAS "'||b||'"'
				when a="END" then do
								ADDRNEW addr
								drop enc
				end
				otherwise
			end
		end
		call close(vcf)
		call close(converttext)
		if converted then address command "Delete T:ImportVCF QUIET"
	end
end
